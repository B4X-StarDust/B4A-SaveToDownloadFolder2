Build1=Default,b4a.example
File1=Layout.bal
FileGroup1=Default Group
Group=Default Group
Library1=core
Library2=javaobject
Library3=xui
Library4=okhttp
ManifestCode='This code will be applied to the manifest file during compilation.~\n~'You do not need to modify it in most cases.~\n~'See this link for for more information: https://www.b4x.com/forum/showthread.php?p=78136~\n~AddManifestText(~\n~<uses-sdk android:minSdkVersion="21" android:targetSdkVersion="34"/>~\n~<supports-screens android:largeScreens="true" ~\n~    android:normalScreens="true" ~\n~    android:smallScreens="true" ~\n~    android:anyDensity="true"/>)~\n~SetApplicationAttribute(android:icon, "@drawable/icon")~\n~SetApplicationAttribute(android:label, "$LABEL$")~\n~CreateResourceFromFile(Macro, Themes.LightTheme)~\n~'End of default text.~\n~CreateResourceFromFile(Macro, Core.NetworkClearText)
Module1=Starter
NumberOfFiles=1
NumberOfLibraries=4
NumberOfModules=1
Version=13.1
@EndOfDesignText@
#Region  Project Attributes 
	#ApplicationLabel: SaveToDownloadFolder
	#VersionCode: 1
	#VersionName: 1
	'SupportedOrientations possible values: unspecified, landscape or portrait.
	#SupportedOrientations: unspecified
	#CanInstallToExternalStorage: False
#End Region

#BridgeLogger: True

#Region  Activity Attributes 
	#FullScreen: False
	#IncludeTitle: True
#End Region

Sub Process_Globals
	'These global variables will be declared once when the application starts.
	'These variables can be accessed from all modules.
	Private xui As XUI
End Sub

Sub Globals
	'These global variables will be redeclared each time the activity is created.
End Sub

Sub Activity_Create(FirstTime As Boolean)
	Activity.LoadLayout("Layout")
End Sub

Sub Activity_Resume

End Sub

Sub Activity_Pause (UserClosed As Boolean)

End Sub

Sub Button1_Click
	'DownloadAndSave("https://www.b4x.com/images/Logo_on-dark.png")
	'DownloadAndSave("http://www.s-dev.it/favicon.ico")
	DownloadAndSave("https://www.b4x.com/android/forum/data/avatars/m/101/101440.jpg")
End Sub

Sub DownloadAndSave(Address As String)
	Dim fn As String = Address.SubString(Address.lastIndexOf("/")+1)
	Dim HttpClient As OkHttpClient
	Dim HttpRequest As OkHttpRequest
	
	HttpRequest.InitializeGet(Address)
	HttpClient.Initialize("hc")
	HttpClient.Execute(HttpRequest , "1")
	wait for hc_ResponseSuccess (Response As OkHttpResponse, TaskId As Int)
	Response.GetAsynchronously("response",File.OpenOutput(File.DirInternal,fn, False),True,"1")
	
	Wait For Response_StreamFinish (Success As Boolean, TaskId As Int)
	If Success Then
		Log("Download " & fn)
		Log("lengh: " & File.Size(File.DirInternal,fn))
		SaveFileToDownloads(fn)
	Else
		LogColor(LastException.Message,Colors.Magenta)
		xui.MsgboxAsync(LastException.Message , "Error")
	End If

End Sub

Sub SaveFileToDownloads(FileName As String)
	Dim mimeType As String  = ResolveMime(FileName)
	Dim ctxt As JavaObject
	ctxt.InitializeContext

	Dim ContentValues As JavaObject
	ContentValues.InitializeNewInstance("android.content.ContentValues", Null)
	'ContentValues.RunMethod("put", Array("display_name", FileName)) ' Imposta il nome corretto
	ContentValues.RunMethod("put", Array("title", FileName)) ' Nome file
	ContentValues.RunMethod("put", Array("mime_type", mimeType)) ' Tipo MIME
	ContentValues.RunMethod("put", Array("relative_path", "Download/")) ' Cartella di destinazione

	' Ottenere il MediaStore URI per i Download
	Dim resolver As JavaObject = ctxt.RunMethod("getContentResolver", Null)
	Dim uri As JavaObject = resolver.RunMethod("insert", Array(GetMediaStoreDownloadsUri, ContentValues))

	If uri.IsInitialized Then
		Dim os As OutputStream
		os = resolver.RunMethod("openOutputStream", Array(uri))
		File.Copy2(File.OpenInput(File.DirInternal,FileName),os)
		os.Close
		Log("✅ File salvato con successo in Download: " & FileName)
		ToastMessageShow("File downloaded", False)
	Else
		Log("❌ Errore nel salvataggio del file")
		ToastMessageShow("ERROR", False)
	End If
End Sub

' Funzione per ottenere l'URI corretto di MediaStore.Downloads
Sub GetMediaStoreDownloadsUri As Object
	Dim jo As JavaObject
	jo.InitializeStatic("android.provider.MediaStore$Downloads")
	Return jo.GetField("EXTERNAL_CONTENT_URI")
End Sub

Private Sub ResolveMime(fn As String) As String
	Dim M As Map =  CreateMap("htm":"text/html","html":"text/html","xml":"text/xml","txt":"text/plain","pdf":"application/pdf", _
	"css":"text/css", "csv":"text/csv", "js":"text/javascript", "json":"application/json", _
	"doc":"application/msword", "docx":"application/vnd.openxmlformats-officedocument.wordprocessingml.document", _
	"xls":"application/vnd.ms-excel", "xlsx":"application/vnd.openxmlformats-officedocument.spreadsheetml.sheet", _
	"odt":"application/vnd.oasis.opendocument.text", "ods":"application/vnd.oasis.opendocument.spreadsheet", _
	"epub":"application/epub+zip", "jar":"application/java-archive", "rar":"application/vnd.rar", "zip":"application/zip", _
	"jpeg":"image/jpeg","jpg":"image/jpeg","bmp":"image/bmp","png":"image/png", "gif":"image/gif", _
	"ico":"image/x-icon", "mp3":"audio/mpeg", "avi":"video/x-msvideo", "mpeg":"video/mpeg")
	
	If fn.IndexOf(".")>-1 Then 
		Dim exten As String = fn.SubString(fn.LastIndexOf(".")+1)
		Return M.GetDefault(exten,"application/octet-stream")
	Else
		Return "application/octet-stream"
	End If
End Sub